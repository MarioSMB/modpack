
// ========================================================================
//   Parser
// ========================================================================
entity parse_block();
entity parse_expression();

entity parse_identifier(string id)
{
	if ( lex_lookahead.script_type == LEX_OPERATOR_OTHER &&
		lex_lookahead.script_value == "(" )
	{
		entity function_call = ast_function(id);
		lex_get_token(); 
		if ( lex_lookahead.script_type == LEX_OPERATOR_OTHER && lex_lookahead.script_value == ")" )
			lex_get_token(); 
		else
		{
			entity param = NULL;
			entity nextparam;
			while ( lex_lookahead.script_type != LEX_EOF )
			{
				nextparam = parse_expression();
				if ( !nextparam )
					break;
				if ( !param )
					function_call.ast_operand1 = nextparam;
				else
					param.script_next = nextparam;
				param = nextparam;
				
				if ( lex_lookahead.script_type == LEX_OPERATOR_OTHER &&
					lex_lookahead.script_value == ")" )
				{
					lex_get_token();
					break;
				}
				
				lex_get_token(); 
			}
		}
		return function_call;
	}
	return ast_variable(id);
}

entity parse_primary()
{
	entity lex_prev = lex_move_lookahead();
	lex_get_token();
	entity result = NULL;
	switch(lex_prev.script_type)
	{
		case LEX_IDENTIFIER:
			result = parse_identifier(lex_prev.script_value);
			break;
		case LEX_VALUE_NUMBER:
		case LEX_VALUE_STRING:
			result = ast_simplevalue(lex_prev.script_value);
			break;
		case LEX_OPERATOR_ARITH:
			result = parse_primary();
			if ( lex_prev.script_value == "-" )
				result = ast_unaryminus(result);
			break;
		case LEX_OPERATOR_OTHER:
			if ( lex_prev.script_value == "(" )
			{
				result = parse_block();
				lex_get_token(); // remove )
			}
			break;
		case LEX_OPERATOR_BIT:
			if ( lex_prev.script_value == "~" )
			{
				result = parse_primary();
				result = ast_bitnot(result);
			}
			break;
		case LEX_OPERATOR_LOGICAL:
			if ( lex_prev.script_value == "!" )
			{
				result = parse_primary();
				result = ast_not(result);
			}
			break;
		case LEX_OPERATOR_INCREMENT:
			if ( lex_lookahead.script_type == LEX_IDENTIFIER )
			{
				entity (entity, entity) operator = ast_sum;
				if ( lex_prev.script_value == "--" )
					operator = ast_sub;
				result = ast_assign_operator(ast_variable(lex_lookahead.script_value),
					ast_simplevalue("1"),operator);
				lex_get_token();
			}
			break;
		case LEX_KEYWORD:
			if ( lex_prev.script_value == "return" )
			{
				if ( lex_lookahead.script_type == LEX_OPERATOR_COMMA )
					lex_get_token(); // remove ,;
				else
					result = parse_expression();
				result = ast_return(result);
			}
			else if ( lex_prev.script_value == "break" )
			{
				result = ast_break();
			}
			else if ( lex_prev.script_value == "continue" )
			{
				result = ast_continue();
			}
			break;
	}
	delete(lex_prev);
	return result;
}

entity parse_field()
{
	entity val = parse_primary();
	while(lex_lookahead.script_type == LEX_OPERATOR_OTHER && 
		lex_lookahead.script_value == "." && val.ast_lvalue )
	{
		lex_get_token();
		val = ast_field(val,lex_lookahead.script_value);
		lex_get_token();
	}
	return val;
}

entity parse_multiplication()
{
	entity val = parse_field();
	entity lex_prev;
	while(lex_lookahead.script_type == LEX_OPERATOR_MULT)
	{
		lex_prev = lex_move_lookahead();
		lex_get_token();
		if ( lex_prev.script_value == "/" )
			val = ast_div(val,parse_field());
		else if ( lex_prev.script_value == "%" )
			val = ast_mod(val,parse_field());
		else
			val = ast_mul(val,parse_field());
		delete(lex_prev);
	}
	return val;
}

entity parse_arithmetic()
{
	entity val = parse_multiplication();
	entity lex_prev;
	while(lex_lookahead.script_type == LEX_OPERATOR_ARITH)
	{
		lex_prev = lex_move_lookahead();
		lex_get_token();
		if ( lex_prev.script_value == "-" )
			val = ast_sub(val,parse_multiplication());
		else
			val = ast_sum(val,parse_multiplication());
		delete(lex_prev);
	}
	return val;
}

entity parse_bitwise()
{
	entity val = parse_arithmetic();
	entity lex_prev;
	while(lex_lookahead.script_type == LEX_OPERATOR_BIT)
	{
		lex_prev = lex_move_lookahead();
		lex_get_token();
		if ( lex_prev.script_value == "<<" )
			val = ast_bitlshift(val,parse_arithmetic());
		else if ( lex_prev.script_value == ">>" )
			val = ast_bitrshift(val,parse_arithmetic());
		else if ( lex_prev.script_value == "&" )
			val = ast_bitand(val,parse_arithmetic());
		else if ( lex_prev.script_value == "^" )
			val = ast_bitxor(val,parse_arithmetic());
		else
			val = ast_bitor(val,parse_arithmetic());
		delete(lex_prev);
	}
	return val;
}

entity parse_comparison()
{
	entity val = parse_bitwise();
	entity lex_prev;
	while(lex_lookahead.script_type == LEX_OPERATOR_COMPARE)
	{
		lex_prev = lex_move_lookahead();
		lex_get_token();
		if ( lex_prev.script_value == "<" )
			val = ast_lt(val,parse_bitwise());
		else if ( lex_prev.script_value == "<=" )
			val = ast_le(val,parse_bitwise());
		else if ( lex_prev.script_value == "!=" )
			val = ast_ne(val,parse_bitwise());
		else if ( lex_prev.script_value == ">=" )
			val = ast_ge(val,parse_bitwise());
		else if ( lex_prev.script_value == ">" )
			val = ast_gt(val,parse_bitwise());
		else
			val = ast_eq(val,parse_bitwise());
		delete(lex_prev);
	}
	return val;
}

entity parse_boolean()
{
	entity val = parse_comparison();
	entity lex_prev;
	while(lex_lookahead.script_type == LEX_OPERATOR_LOGICAL)
	{
		lex_prev = lex_move_lookahead();
		lex_get_token();
		if ( lex_prev.script_value == "||" )
			val = ast_or(val,parse_comparison());
		else
			val = ast_and(val,parse_comparison());
		delete(lex_prev);
	}
	return val;
}

entity parse_expression()
{
	entity val = parse_boolean();
	
	if ( lex_lookahead.script_type == LEX_OPERATOR_OTHER && 
		lex_lookahead.script_value == "?" )
	{
		lex_get_token();
		entity branch_true = parse_boolean();
		entity branch_false;
		if ( lex_lookahead.script_type == LEX_OPERATOR_OTHER && 
				lex_lookahead.script_value == ":" )
		{
			lex_get_token();
			branch_false = parse_boolean();
		}
		else
			branch_false = ast_simplevalue("");
		
		return ast_if(val,branch_true,branch_false);
	}
	
    return val;
}

entity parse_assignment()
{
	entity lval = parse_expression();
	if ( lex_lookahead.script_type == LEX_OPERATOR_ASSIGN && lval.ast_lvalue )
	{
			string oper = substring(lex_lookahead.script_value,0,1);
			lex_get_token();
			entity rval = parse_expression();
			entity(entity,entity) operator;
			if ( oper == "+" )
				operator = ast_sum;
			else if ( oper == "-" )
				operator = ast_sub;
			else if ( oper == "*" )
				operator = ast_mul;
			else if ( oper == "/" )
				operator = ast_div;
			else if ( oper == "%" )
				operator = ast_mod;
			else if ( oper == "^" )
				operator = ast_bitxor;
			else if ( oper == "&" )
				operator = ast_bitand;
			else if ( oper == "|" )
				operator = ast_bitor;
			else
				return ast_assign(lval,rval);
			return ast_assign_operator(lval,rval,operator);
	}
	else if ( lex_lookahead.script_type == LEX_OPERATOR_INCREMENT && lval.ast_lvalue )
	{
		entity (entity, entity) operator = ast_sum;
		if ( lex_lookahead.script_value == "--" )
			operator = ast_sub;
		lex_get_token();
		return ast_assign_operator(lval,ast_simplevalue("1"),operator);
	}
	
	return lval;
}

entity parse_statement()
{
	if ( lex_lookahead.script_type == LEX_KEYWORD )
	{
		if ( lex_lookahead.script_value == "if" )
		{
			lex_get_token(); // remove if
			lex_get_token(); // remove (
			entity condition = parse_assignment();
			lex_get_token(); // remove )
			entity branch_true = parse_statement();
			entity branch_false;
			if ( lex_lookahead.script_type == LEX_KEYWORD && lex_lookahead.script_value == "else" )
			{
				lex_get_token(); // remove else
				branch_false = parse_statement();
			}
			else
				branch_false = ast_simplevalue("");
			
			return ast_if(condition,branch_true,branch_false);
		}
		else if ( lex_lookahead.script_value == "while" )
		{
			lex_get_token(); // remove while
			lex_get_token(); // remove (
			entity condition = parse_assignment();
			lex_get_token(); // remove )
			
			return ast_while(condition,parse_statement());
		}
		else if ( lex_lookahead.script_value == "for" )
		{
			lex_get_token(); // remove for
			lex_get_token(); // remove (
			entity start = parse_assignment();
			lex_get_token(); // remove ;
			entity condition = parse_assignment();
			lex_get_token(); // remove ;
			entity increment = parse_assignment();
			lex_get_token(); // remove )
			
			// { block; increment }
			entity for_inner_block = ast_block();
			for_inner_block.ast_operand1 = parse_statement();
			if ( for_inner_block.ast_operand1 )
				for_inner_block.ast_operand1.script_next = increment;
			else
				for_inner_block.ast_operand1 = increment;
			
			// { start; while ( condition ) inner_block }
			entity for_outer_block = ast_block();
			for_outer_block.ast_operand1 = start;
			start.script_next = ast_while(condition,for_inner_block);
			
			return for_outer_block;
		}
		else if ( lex_lookahead.script_value == "function" )
		{
			lex_get_token(); // remove function
			string id = lex_lookahead.script_value;
			lex_get_token(); // remove id
			lex_get_token(); // remove (
			entity func_declaration = ast_function_declaration(id);
			
			if ( lex_lookahead.script_type == LEX_OPERATOR_OTHER && lex_lookahead.script_value == ")" )
				lex_get_token(); 
			else
			{
				entity param = NULL;
				entity nextparam;
				while ( lex_lookahead.script_type != LEX_EOF )
				{
					nextparam = spawn();
					nextparam.script_value = lex_lookahead.script_value;
					lex_get_token(); // remove param name
					if ( lex_lookahead.script_type == LEX_OPERATOR_ASSIGN && 
						lex_lookahead.script_value == "=" )
					{
						lex_get_token(); // remove =
						nextparam.ast_operand1 = parse_expression();
					}
					if ( !param )
						func_declaration.ast_operand1 = nextparam;
					else
						param.script_next = nextparam;
					param = nextparam;
					
					if ( lex_lookahead.script_type == LEX_OPERATOR_OTHER &&
						lex_lookahead.script_value == ")" )
					{
						lex_get_token();
						break;
					}
					
					lex_get_token();
					
				}
			}			
			func_declaration.ast_operand2 = parse_statement();
			
			return func_declaration;
		}
	}
	
	
	entity val = parse_assignment();
	if ( lex_lookahead.script_type == LEX_OPERATOR_COMMA )
		lex_get_token(); // skip ; and ,
	return val;
}

entity parse_block()
{
	entity val = ast_block();
	entity prev = NULL;
	entity next;
	while ( lex_lookahead.script_type != LEX_EOF && 
		!( lex_lookahead.script_type == LEX_OPERATOR_OTHER && lex_lookahead.script_value == ")" ) )
	{
		next = parse_statement();
		if ( !next )
			break;
		if ( prev )
			prev.script_next = next;
		else
			val.ast_operand1 = next;
		prev = next;
	}
	return val;
}

entity script_compile(string code_source)
{
	lex_string_index = 0;
	lex_string_end = strlen(code_source);
	lex_string = code_source;
	lex_get_token();
	entity ast = parse_block();
	delete(lex_lookahead);
	lex_lookahead = NULL;
	//script_debug_ast(ast);
	return ast;
}
