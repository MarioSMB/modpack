#pragma once

MSG_INFO_NOTIF(POWERUP_STAR,                        1,      1, 0, "s1", "s1",       "star",                         "^BG%s^K1 picked up Starman", "")
MSG_CENTER_NOTIF(POWERUP_STAR,                      1,      0, 0, "",               CPID_POWERUP,           "0 0",  "^F2Starman gives you complete invulnerability!", "")
MSG_CENTER_NOTIF(POWERDOWN_STAR,                    1,      0, 0, "",               CPID_POWERUP,           "0 0",  "^F2Starman has faded", "")

#ifdef GAMEQC
const int IT_STAR = BIT(23);

MODEL(Star_ITEM, Item_Model("star.md3"));
SOUND(Star, "items/powerup_star");
#endif

#ifdef SVQC
.float star_touch_time;
.vector star_oldcolormod;

.float star_finished;
AUTOCVAR(g_powerups_star, bool, true, "");
AUTOCVAR(g_powerup_star_time, float, 33.616, "");
AUTOCVAR(g_powerup_star_touch_delay, float, 0.2, "");
AUTOCVAR(g_powerup_star_speed, float, 1.7, "");
void powerup_star_init(Pickup def, entity item)
{
    if(!autocvar_g_powerups || !autocvar_g_powerups_star)
        def.spawnflags |= ITEM_FLAG_MUTATORBLOCKED;

    if(!item.star_finished)
        item.star_finished = (item.count) ? item.count : autocvar_g_powerup_star_time;
}
#endif
REGISTER_ITEM(Star, Powerup) {
    this.m_canonical_spawnfunc = "item_starman";
#ifdef SVQC
    this.m_iteminit         =   powerup_star_init;
#endif
#ifdef GAMEQC
    this.spawnflags         =   ITEM_FLAG_NORMAL;
    this.m_itemid           =   IT_STAR;
    this.m_model            =   MDL_Star_ITEM;
    this.m_sound            =   SND_Star;
    this.m_respawnsound     =   SND_STRENGTH_RESPAWN;
#endif
    this.netname            =   "starman";
    this.m_name             =   _("Starman");
    this.m_icon             =   "starman";
    this.m_color            =   '1 1 0';
    this.m_waypoint         =   _("Starman");
    this.m_waypointblink    =   2;
}

SPAWNFUNC_ITEM(item_starman, ITEM_Star)

CLASS(Star, Powerups)
    ATTRIB(Star, netname, string, "starman");
    ATTRIB(Star, m_name, string, _("Starman"));
    ATTRIB(Star, m_icon, string, "starman");
ENDCLASS(Star)
REGISTER_STATUSEFFECT(Star, NEW(Star));
